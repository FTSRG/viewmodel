package hu.bme.mit.inf.viewmodel.runtime.queries.manifestationtrace

import hu.bme.mit.inf.viewmodel.runtime.queries.logicmodel.eObjectConstantValue
import hu.bme.mit.inf.viewmodel.runtime.queries.logicmodel.javaObjectConstantValue
import hu.bme.mit.inf.viewmodel.runtime.queries.logicmodel.concreteRelation
import hu.bme.mit.inf.viewmodel.runtime.queries.logicmodel.concreteVariable

import "http://inf.mit.bme.hu/emf/2017/LogicModel"
import "http://inf.mit.bme.hu/emf/2017/ManifestationTrace"
// Must not use "http://www.eclipse.org/emf/2002/Ecore",
// because Xcore already loads Ecore.ecore into the ResourceSet with a platform resource URI.
import "platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"

pattern manifestableRelation(TraceModelId : EString, LeftManifestation : InterpretedManifestation, RightManifestation : Manifestation, Relation : EStructuralFeature) {
	find properlyManifested(TraceModelId, LeftRep, LeftManifestation);
	find properlyManifested(TraceModelId, RightRep, RightManifestation);
	find concreteRelation(LeftRep, RightRep, Relation);
}

pattern manifestableConcreteVariable(TraceModelId : EString, Rep : Variable, Type : EClass) {
	find tracedVariable(TraceModelId, Rep);
	find concreteVariable(Rep, Type);
}

pattern manifestableEObjectConstantValue(TraceModelId : EString, Rep : Variable, Value : EObject) {
	find tracedVariable(TraceModelId, Rep);
	find eObjectConstantValue(Rep, Value);
}

pattern manifestableJavaObjectConstantValue(TraceModelId : EString, Rep : Variable, Value : java Object) {
	find tracedVariable(TraceModelId, Rep);
	find javaObjectConstantValue(Rep, Value);
}

private pattern properlyManifested(TraceModelId : EString, Rep : Variable, Man : Manifestation) {
	find manifestation(TraceModelId, Rep, Man);
	find concreteVariable(Rep, Type);
	InterpretedManifestation.type(Man, Type);
} or {
	find manifestation(TraceModelId, Rep, Man);
	find eObjectConstantValue(Rep, Value);
	UninterpretedManifestation.sourceEObject(Man, Value);
} or {
	find manifestation(TraceModelId, Rep, Man);
	find javaObjectConstantValue(Rep, Value);
	PrimitiveManifestation.value(Man, Value);
}

pattern manifestation(TraceModelId : EString, Rep : Variable, Manifestation : Manifestation) {
	ManifestationTrace.traceModelId(Trace, TraceModelId);
	ManifestationTrace.manifestations(Trace, Manifestation);
	Manifestation.variable(Manifestation, Rep);
}

private pattern tracedVariable(TraceModelId : EString, Var : Variable) {
	ManifestationTrace.traceModelId(Trace, TraceModelId);
	ManifestationTrace.logicModel.variables(Trace, Var);
}
